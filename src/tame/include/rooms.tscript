
extend room r_intro
{
	override function OnRoomEntered()
	{
		Passages.IntroSplash();
	}

	override function OnLookAround()
	{
		// Nothing!
	}

	override function OnFirstVisit()
	{
		Passages.IntroText();
	}

	override function OnReVisit()
	{
		// Should not happen.
	}

	// Called to just move to the start.
	onAction(a_gosouth)
	{
		SetRoom(player, r_northwestroom);
	}
}

extend room r_northwestroom
{
	init()
	{
		GiveObject(this, o_exitdoor);
		GiveObject(this, o_exitdoorplacard);
		GiveObject(this, o_ouroboros);
	}

	override function OnRoomEntered()
	{
		... "You are in the starting room.\n";
		if (world.cycled && !blurb)
		{
			blurb = true;
			wait(1000);
			... "...";
			wait(1000);
			Passages.BackToStart();
		}

	}

	override function OnFirstVisit()
	{
		... `Ugh. You're really sore from the fall. Who the heck puts a makeshift
			dungeon all the way out here, anyway? That lingering mildewy small doesn't
			help things, either.`;
	}

	override function OnLookAround()
	{
		... `The room is cramped and damp. It must not have been exposed to the outside world in quite a while.`;
		... "....Well, until YOU came along at least!";
		... "";
		Tips.Show(TIP_LOOKAT);
	}

	override function OnReVisit()
	{
		Tips.show(TIP_LOOKAROUND);
		if (o_floorplate.used)
			Tips.Show(TIP_TAKE);
	}

	override function OnDirections()
	{
		... "It looks like you can GO SOUTH from here.";
		Tips.Show(TIP_GO);
	}

	onAction(a_gosouth)
	{
		SetRoom(player, r_westroom);
	}

	onAction(a_goeast)
	{
		if (!world.cycled)
			p_main.CantGo();
		else
			SetRoom(player, r_northroom);
	}

}

extend room r_westroom
{
	init()
	{
		GiveObject(this, o_floorplate);
		GiveObject(this, o_irondoor);
	}

	onAction(a_gonorth)
	{
		SetRoom(player, r_northwestroom);
	}

	onAction(a_gosouth)
	{
		if (HasObject(this, o_irondoor))
			textln("You can't - an iron door blocks the way.");
		else
			SetRoom(player, r_southwestroom);
	}

	override function OnRoomEntered()
	{
		... "You are in the west room.";
	}

	override function OnFirstVisit()
	{
		... "It's kinda chilly in here.";
	}

	override function OnLookAround()
	{
		if (HasObject(this, o_irondoor))
			... "It's a pretty bare room, save for a single iron door.";
		else
			... "It's a pretty bare room, save for the iron door, which is not there, anymore.";
		Tips.Show(TIP_USE);
	}

	override function OnReVisit()
	{
		... "It's still kinda chilly in here.";
	}

	override function OnDirections()
	{
		if (HasObject(this, o_irondoor))
			... "You can GO NORTH from here. An iron door blocks the way south.";
		else
			... "You can GO NORTH or SOUTH from here.";
	}

}

extend room r_southwestroom
{
	init()
	{
		GiveObject(this, o_basin);
	}

	onAction(a_gonorth)
	{
		SetRoom(player, r_westroom);
	}

	onAction(a_goeast)
	{
		SetRoom(player, r_southroom);
	}

	override function OnRoomEntered()
	{
		... "You are in the Southwest room.";
	}

	override function OnFirstVisit()
	{
		... "You have a weird feeling that this room may be important.";
	}

	override function OnLookAround()
	{
		... `The walls are a bright marble. Around the top of the walls, there are bas-relief carvings of snakes,
			such that each snake seems poised to eat the tail of the snake in front of it.`;
	}

	override function OnReVisit()
	{
		// TODO: Finish.
	}

	override function OnDirections()
	{
		... "You can go NORTH or EAST from here.";
	}

}

extend room r_southroom
{
	init()
	{
		GiveObject(this, o_jaggedstone);
		GiveObject(this, o_smoothstone);
	}

	onAction(a_gowest)
	{
		SetRoom(player, r_southwestroom);
	}

	onAction(a_goeast)
	{
		SetRoom(player, r_southeastroom);
	}

	override function OnRoomEntered()
	{
		... "You are in the southern room.";
	}

	override function OnFirstVisit()
	{
		... "Uh oh - watch your step!";
	}

	override function OnLookAround()
	{
		... "For some reason, there was a cave-in here and a bunch of rubble was scattered everywhere.";
		... "There's a pool of water here, too.";
	}

	override function OnReVisit()
	{
		... "You defly navigate the rocky floor.";
	}

	override function OnDirections()
	{
		... "You can go WEST or EAST from here.";
		Tips.Show(TIP_GOSHORT);
	}

}

extend room r_southeastroom
{
	init()
	{
		GiveObject(this, o_statue);
		GiveObject(this, o_cup);
		GiveObject(this, o_ladle);
	}

	onAction(a_gowest)
	{
		SetRoom(player, r_southroom);
	}

	onAction(a_gonorth)
	{
		SetRoom(player, r_eastroom);
	}

	override function OnRoomEntered()
	{
		... "You are in the Southeast Room.";
	}

	override function OnFirstVisit()
	{
		... "GAH! WHAT THE-! Oh. It's a statue of some kind of sagely-looking man.";
	}

	override function OnLookAround()
	{
		... "You are in stone room with a brick floor.";
	}

	override function OnReVisit()
	{
		... "You say hello and wave to the petrified man.";
	}

	override function OnDirections()
	{
		... "You can go WEST or NORTH from here.";
	}
}

extend room r_eastroom
{
	init()
	{
		GiveObject(this, o_insets);
		GiveObject(this, o_prisms);
		GiveObject(this, o_magentaeyedoor);
	}

	onAction(a_gonorth)
	{
		if (HasObject(this, o_magentaeyedoor))
			p_main.CantGo();
		else
			SetRoom(player, r_northeastroom);
	}

	onAction(a_gosouth)
	{
		SetRoom(player, r_southeastroom);
	}

	override function OnRoomEntered()
	{
		... "You enter the eastern room.";
	}

	override function OnFirstVisit()
	{
		... `Now this is strange (well, stranger than what you've seen before? probably not) - 
			there's some kind of contrived contraption, here!`;
	}

	override function OnLookAround()
	{
		... `Maybe somebody set this up because they too were lost? Oh who are you kidding - 
			this is another dumb puzzle.`;
	}

	override function OnReVisit()
	{
		... "You know, with the stands and prisms and such.";
	}

	override function OnDirections()
	{
		if (HasObject(this, o_magentaeyedoor))
			... "You can go SOUTH from here, but the NORTH is blocked by a door.";
		else
			... "You can go NORTH or SOUTH from here.";
	}
}

extend room r_northeastroom
{
	init()
	{
		GiveObject(this, o_stonestatue);
		GiveObject(this, o_stonedoor);
	}

	onAction(a_gowest)
	{
		if (HasObject(this, o_stonedoor))
			p_main.CantGo();
		else
			SetRoom(player, r_northroom);
	}

	onAction(a_gosouth)
	{
		SetRoom(player, r_eastroom);
	}

	override function OnRoomEntered()
	{
		... "You are in the northeast room.";
	}

	override function OnFirstVisit()
	{
		... "Aw crap - more weird statues.";
	}

	override function OnLookAround()
	{
		... "It's a regular stone room with a dirt floor. Whoever made this dungeon is running out of ideas.";
	}

	override function OnReVisit()
	{
		// Nothing.
	}

	override function OnDirections()
	{
		if (HasObject(this, o_stonedoor))
			... "You can go SOUTH from here - a door is blocking the way WEST.";
		else
			... "You can go WEST or SOUTH from here.";
	}

}

extend room r_northroom
{
	init()
	{
		GiveObject(this, o_pedestal);
	}

	onAction(a_gowest)
	{
		if (!HasObject(this, o_portal))
			p_main.CantGo();
		else 
		{
			... "You gird your loins as much as loins can be girded, and step through the portal.";
			pause();
			SetRoom(player, r_northwestroom);
			world.cycled = true;
		}
	}

	onAction(a_goeast)
	{
		SetRoom(player, r_northeastroom);
	}

	override function OnRoomEntered()
	{
		... "You are in the northeast room.";
	}

	override function OnFirstVisit()
	{
		... "Oh wow - this is new.";
	}

	override function OnLookAround()
	{
		... `The room's walls are covered ceiling to floor with maps of varying age and accuracy.
			Maybe this was some explorer's dungeon.`;
	}

	override function OnReVisit()
	{
		// Nothing.
	}

	override function OnDirections()
	{
		if (!HasObject(this, o_portal))
			... "You can go WEST or EAST from here.";
		else
			... "You can go EAST from here.";
	}
}
